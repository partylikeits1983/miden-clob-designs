use.miden::note
use.miden::contracts::wallets::basic->wallet

const.ASSET=0
const.ASSET_HALF=4
const.ACCOUNT_ID_PREFIX=16
const.ACCOUNT_ID_SUFFIX=17
const.P2ID_TAG=60

#! Inputs: [SERIAL_NUM, SCRIPT_HASH]
proc.build_p2id_recipient_hash
    padw hmerge
    # => [SERIAL_NUM_HASH, SCRIPT_HASH]

    swapw hmerge
    # => [SERIAL_SCRIPT_HASH]

    padw
    mem_load.ACCOUNT_ID_SUFFIX
    mem_load.ACCOUNT_ID_PREFIX
    push.0.0
    # => [0, 0, prefix, suffix, EMPTY_WORD, SERIAL_SCRIPT_HASH]

    push.ACCOUNT_ID_PREFIX mem_storew dropw
    push.20 mem_storew dropw

    push.8.16
    # => [ptr, elements]

    exec.note::compute_inputs_hash
    # => [INPUTS_HASH, SERIAL_SCRIPT_HASH]

    hmerge
    # => [P2ID_RECIPIENT]
end

begin
    # => [account_prefix, account_suffix, tag]
    mem_store.ACCOUNT_ID_PREFIX
    mem_store.ACCOUNT_ID_SUFFIX
    mem_store.P2ID_TAG

    # get asset
    push.ASSET exec.note::get_assets drop drop
    # => []

    mem_loadw.ASSET
    # => [ASSET]

    swap.3 push.2 div swap.3
    # => [ASSET_HALF]

    # store ASSET_HALF @ mem addr 0
    mem_storew.ASSET_HALF dropw
    # => []

    mem_loadw.ASSET
    # => [ASSET]

    call.wallet::receive_asset
    # => []

    # push script hash
    push.5114721150025532103.1324187411136457873.11900545512113364072.1928473465238172547
    # => [P2ID_SCRIPT_HASH]

    # serial num
    push.1.1.1.1
    # => [P2ID_SERIAL_NUM, P2ID_SCRIPT_HASH]

    exec.build_p2id_recipient_hash
    # => [P2ID_RECIPIENT]

    push.1.1.0
    # => [aux, public_note, execution_hint_always, P2ID_RECIPIENT]

    mem_load.P2ID_TAG
    # => [tag, aux, note_type, execution_hint, RECIPIENT]

    call.wallet::create_note
    # => [note_idx, pad(15) ...]

    padw mem_loadw.ASSET_HALF
    # => [ASSET / 2, note_idx]

    call.wallet::move_asset_to_note
    # => [ASSET, note_idx, pad(11)]

    dropw dropw dropw dropw
    # => []
end
